title: 库尔特·罗森菲尔德
is-pinned-card: true
tags: advisor, left
card-image: img/portraits/RosenfeldKurt.jpg
view-if: rosenfeld_advisor = 1
new-page: true 

= 库尔特·罗森菲尔德

罗森菲尔德是柏林的一位律师，专为左翼人士辩护。他支持与KPD合作以及将社民党导向阶级斗争路线。

- @building_the_left
- @kpd_cooperation
- @against_toleration
- @root: 返回主页

@building_the_left
title: 增强左翼
subtitle: 将社民党向左推动
unavailable-subtitle: [? if advisor_action_timer > 0 : 还需[+ advisor_action_timer +]个月才能执行下一个顾问行动。 ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_strength += 6; left_dissent -= 8; reformist_dissent += 2

我们增强了社民党内左派的力量。

@kpd_cooperation
title: 与KPD合作
subtitle: 推动社民党对KPD更加友善。
unavailable-subtitle: [? if advisor_action_timer > 0 : 还需[+ advisor_action_timer +]个月才能执行下一个顾问行动。 ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_strength += 5; kpd_relation += 4*(1-dissent); reformist_dissent += 2; communist_coalition += 1 if kpd_cooperation_seen <= 1; kpd_cooperation_seen += 1

罗森菲尔德在其工作中曾为许多KPD党员辩护，这应该会使得我们之间的联络成为可能。

@against_toleration
title: 反对容忍
view-if: not constructive_vonc
subtitle: 结束我们当前的容忍协定，通过不信任投票推翻政府。
unavailable-subtitle: [? if advisor_action_timer > 0 : 还需[+ advisor_action_timer +]个月才能执行下一个顾问行动。 ?] [? if spd_toleration == 0 : 社民党并未处于容忍中。 ?]
choose-if: advisor_action_timer == 0 and spd_toleration
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 4; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 8

我们已对容忍所带来的麻烦感到厌倦，并向现任少数派政府发起了不信任投票。

新的选举定于[+ next_election_year +]年[+ next_election_month : month +]举行。
