title: 媒体
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg
#subtitle: Setting the direction for our newspapers and radio.

= 媒体

社民党经营着一个真正的媒体帝国，旗下报纸遍布德国每个城市和地区。我们可以利用媒体实现各种目的……

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @radio_4
- @return: 目前我们不应采取任何媒体行动。
- @easy_discard
- @cancel_advisor_action

@commercial
title: 使我们的报纸更加商业化，通过提升其吸引力，吸引更多非党内支持者的读者。
subtitle: 这或许会让意识形态纯粹主义者感到不安，但它可能带来更多资金，并让中产阶层接触到社会主义观点。
on-arrival: resources += 1; commercialized_media += 1; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1-dissent); left_dissent += 10; labor_dissent += 10

我们的商业化报纸能够带来更多资金。然而，左派和劳工派对此表示不满。

@campaign
title: 我们媒体的目的是选出更多的社会民主党人。
subtitle: 我们的报纸将聚焦于选举活动。
on-arrival: campaign_media += 1; workers_spd += 4*(1 - dissent); unemployed_spd += 2*(1-dissent);

我们的宣传攻势主要覆盖那些早已饱受社会主义宣传轰炸的工人阶级，但它总该产生 *某种效果* ——至少我们如此期盼。

@ideological
title: 我们将在报刊上展开生动的意识形态辩论。
subtitle: 所有派系都有表达观点的空间。
on-arrival: left_dissent -= 6; labor_dissent -= 6; center_dissent -= 6; reformist_dissent -= 6; neorevisionist_dissent -= 6

这是我们历来的媒体路线，它成功地消解了党内诸多矛盾。

@radio
title: 为什么不资助一家广播电台呢？
subtitle: -2资源
choose-if: resources >= 2
unavailable-subtitle: 没有足够的资源。
view-if: radio = 0
on-arrival: radio += 1; resources -= 2; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 2*(1 - dissent); labor_dissent += 5; socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent) if pacifism > 1; pro_republic += 3*(1-dissent) if democratization > 1

我们的新电台让我们得以在比以往更亲密的氛围中与民众建立联系，不只是传播社会主义理念，还有音乐及其他流行内容。鉴于多数无产阶级目前无力负担家用收音机，我们正积极拓展中产阶层受众群体——这一现实令我们的劳工同志们深感痛心。

@radio_2
title: 我们必须继续建设我们的广播网络
subtitle: -1资源
choose-if: resources >= 1
unavailable-subtitle: 没有足够的资源。
view-if: radio > 0 and radio <= 3
on-arrival: radio += 1; resources -= 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

我们的新广播网融合了流行节目与政治节目，并已初显成效。

@radio_3
title: 我们必须继续建设我们的广播网络
subtitle: 该项目现已实现财务自给自足，不再需要任何额外资源。
view-if: radio > 3 and radio <= 5 and year <= 1931
on-arrival: radio += 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 4*(1 - dissent); workers_spd += 4*(1 - dissent); rural_spd += 3*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

我们的广播网络已成为传播我们理念的有效工具。

@radio_4
title: 我们应该继续建设我们的广播网络
subtitle: 随着收音机的普及，其新奇感正逐渐消退，收音机在社会中的渗透率也趋于饱和。-1资源
choose-if: resources >= 1
unavailable-subtitle: 没有足够的资源。
view-if: radio > 3 and (radio >= 6 or year >= 1932) and radio <= 9
on-arrival: radio += 1; resources -= 1; new_middle_spd += 3*(1 - dissent); old_middle_spd += 2*(1 - dissent); workers_spd += 3*(1 - dissent); rural_spd += 2*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 2*(1 - dissent); nationalism -= 2*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 2*(1-dissent)*(democratization-2) if democratization > 2

我们持续对广播电台进行投资。商业广播与政治广播日益普及，使得我们在竞争中传播理念变得愈发困难。
