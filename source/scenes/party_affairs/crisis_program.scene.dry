title: 经济危机
new-page: true
is-card: true
view-if: crisis_urgency >= 1 and crisis_program_timer = 0 and economic_plan = 0 and black_thursday_seen == 1
frequency: 200
on-arrival: month_actions += 1; crisis_program_timer = 5
card-image: img/bankrun.jpg
tags: party_affairs, govt_affairs

= 应对经济危机

我们党正为如何应对经济危机而陷入困境。来自中间派的希法亭主张，必须让经济危机自然发展，因为它不过是资本主义周期循环本质的一部分。

劳工派的经济学家弗拉基米尔·沃伊廷斯基主张，我们必须推行大规模公共工程计划以减少失业并刺激需求，其中可能包括赤字支出。他的反对者如希法亭则认为，该计划将导致恶性通货膨胀卷土重来，根本无助于经济复苏。此外，希法亭指出沃伊廷斯基的策略违背了马克思主义原则

所谓的WTB计划，得名于其主要支持者沃伊廷斯基、塔诺和巴德，正在党内受到越来越多的关注。

然而，党内左派认为WTB计划只不过是试图对资本主义的根本缺陷打补丁以延长其寿命。他们主张这场危机正是通过大规模国有化产业终结资本主义的契机，最终将我国引向社会主义。

与此同时，温和改革派拟定了通过公共工程创造就业的有限计划，该计划由税收出资，不涉及赤字支出或大规模经济重组。劳工派认为该计划在规模和雄心方面都存在不足。

- @support_centrist: 支持中间派观点。
- @support_labor: 支持劳工派观点。
- @support_left: 支持左派观点。
- @support_moderate: 支持改革派观点。
- @defer: 暂缓决策，直至我们获得更多信息。

@support_centrist
subtitle: 让经济危机自然发展。
on-arrival: center_strength += 5; center_dissent -= 5; labor_dissent += 10; wtb_support -= 1; left_dissent += 10;

沃伊廷斯基和工会组织激烈抨击中间派的政策。工人正在挨饿，难道我们要冷酷地抛弃他们，转而支持资产阶级的紧缩政策吗？另一方面，中间派则批评沃伊廷斯基向工人许下虚假希望；他们认为没有任何措施能缓解经济萧条。他们还谈到对赤字支出可能引发新一轮恶性通货膨胀的担忧。

我们当前的政策方针是：没有任何措施能缓解经济萧条。


@support_labor
subtitle: 呼吁增加公共工程和财政刺激
unavailable-subtitle: 劳工派不够强大。
choose-if: historical_mode == 0 or (labor_strength >= left_strength and labor_strength >= center_strength and labor_strength >= reformist_strength)
on-arrival: center_dissent += 10; left_dissent += 5; labor_strength += 5; wtb_support += 1;
go-to: plan_adopted if wtb_support >= 3; plan_support if wtb_support < 3

@plan_support

我们对WTB计划的支持疏远了中间派阵营的正统马克思主义者，并在整个政治光谱中遭遇了来自各方势力既反对又支持的复杂局面。左派认为该计划存在缺陷，因为它保留了资本主义制度；而部分改革派则持谨慎乐观态度。

我们还没有说服全党正式采纳该计划。还需要做更多工作。

@plan_adopted
on-arrival: wtb_adopted = 1; labor_dissent -= 15; reformist_dissent -= 10; labor_strength += 10; center_strength -= 10; center_dissent += 20; left_dissent += 5; economic_plan = 1

通过我们的努力，我们终于说服了党内大多数人采纳WTB计划！[? if spd_in_government == 1 : 只要我们掌握了经济部或财政部，就可以立刻开始实施该计划。?][? if spd_in_government = 0 : 我们应该在选举宣传中重点强调该计划。?]

@support_left
subtitle: Call for mass nationalizations and the transformation of the economy.
unavailable-subtitle: 左派不够强大。
choose-if: historical_mode == 0 or (left_strength >= labor_strength and left_strength >= center_strength and left_strength >= reformist_strength)
on-arrival: left_strength += 10; reformist_dissent += 5; labor_dissent += 5; nationalization_support += 2
go-to: support_left_progress if nationalization_support < 3; adopt_left if nationalization_support >= 3

@support_left_progress

我们试图说服全党支持一项大规模国有化计划以应对经济危机。左派主张该政策符合我们长期的社会主义纲领，而改革派和部分工会人士则认为该计划不切实际，我们需要专注于更直接地改善工人生活。

我们还没有正式采纳该计划。还需要做更多工作。

@adopt_left
on-arrival: left_strength += 15; left_dissent -= 10; reformist_dissent += 10; neorevisionist_dissent += 8; labor_dissent += 6; nationalization_adopted = 1; coup_progress += 1; economic_plan = 3; kpd_relation += 5*(1-dissent)

通过我们的努力，我们终于说服了党内大多数人采纳社会主义经济计划！[? if spd_in_government == 1 : 只要我们掌握了经济部或财政部，就可以立刻开始实施该计划。?]资产阶级很可能会激烈反对。[? if spd_in_government == 0 : 考虑到我们目前的状况，可能难以实施该计划。?]

@support_moderate
subtitle: 呼吁实施无须赤字支出的小规模岗位创造计划。
unavailable-subtitle: 改革派不够强大。
choose-if: historical_mode == 0 or (reformist_strength >= left_strength and reformist_strength >= center_strength and reformist_strength >= labor_strength)
on-arrival: moderate_plan_support += 1; reformist_strength += 8; reformist_dissent -= 5; left_dissent += 6; labor_dissent += 5
go-to: adopt_moderate if moderate_plan_support >= 2; support_moderate_progress if moderate_plan_support < 2

@support_moderate_progress

我们试图说服该党支持一项不依赖赤字支出的温和就业计划。温和方案的支持者认为，该计划最有可能获得资产阶级政党的支持，且在短期内最具可行性。来自左派和劳工派的反对者则批评该计划目标不够宏大。

我们还没有正式采纳该计划。还需要做更多工作。

@adopt_moderate
on-arrival: reformist_strength += 10; reformist_dissent -= 10; left_dissent += 8; labor_dissent += 6; moderate_plan_adopted = 1; economic_plan = 2; coalition_dissent -= 1 if unemployed > 15; z_relation += 5;

通过我们的努力，我们终于说服了党内大多数人采纳温和岗位创造计划！[? if spd_in_government == 1 : 只要我们掌握了经济部或财政部，就可以立刻开始实施该计划。?]

@defer

我们暂缓作出经济政策决策，等待更多信息。沃伊廷斯基及其支持者认为，不作为本身就是一种行动。
